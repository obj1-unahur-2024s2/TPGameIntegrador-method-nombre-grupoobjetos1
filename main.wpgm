import disparo.*
import jugador.*
import enemigo.*
import wollok.game.*

program Main {
  game.width(13)
  game.height(13)

  levelManager.nivelActual().iniciar()
  game.boardGround(levelManager.nivelActual().background())

  game.start()
} 

object menuInicio{
  method background() = "menu_inicio_placeholder.png"
  var property haIniciado = false
  method iniciar() {
    game.boardGround(self.background())
    pantallaGameOver.haIniciado(false)
    levelManager.nivelActual(self)
    self.esperarInput()
  }
  method esperarInput() {
    keyboard.space().onPressDo({self.iniciarJuego()})
  }
  method iniciarJuego() {
    if (not haIniciado){
      haIniciado = true
      nivel1.iniciar()
    }
  }

}

object pantallaFinal{
  method iniciar() {
    game.say(jugador, "Ganaste!")
  }
}

object pantallaGameOver{
  method background() = "menu_gameOver_placeholder.png"
  var property haIniciado = false
  method iniciar() {
    levelManager.nivelActual(self)
    game.clear()
    self.esperarInput()
    game.boardGround(self.background())
  }
  method esperarInput(){
    if (not haIniciado){
      haIniciado = true
      menuInicio.haIniciado(false)
      keyboard.space().onPressDo({menuInicio.iniciar()})
    }
  }
}

object levelManager {
  var property nivelActual = menuInicio
  var property enemigosVivos = 0
  method eliminarEnemigoDeContador() {
    enemigosVivos -= 1
    self.checkEnemigosVivos()
  }
  method checkEnemigosVivos() {
    if(enemigosVivos == 0){
      game.say(jugador, "Nivel terminado")
      game.schedule(3000, {nivelActual.siguiente().iniciar()})
    }
  }
  method checkVidasJugador(){
    if (jugador.vidas() <= 0){
      game.schedule(1000, {pantallaGameOver.iniciar()})
    }
  }
}

object nivel1 {
  method siguiente() = nivel2
  method background() = "Background_forest.png"
  method iniciar() {
    game.boardGround(self.background())
    jugador.vidas(3)
    levelManager.nivelActual(self)
    game.addVisual(jugador)
    jugador.controlar()

    const enemigo1 = new Enemigo(id = 1, position = game.at(6, 12), vida = 3, velocidad = 500)
    enemigo1.iniciar()

    levelManager.enemigosVivos(1)
  }
}

object nivel2 {
  method background() = "Background_forest.png"
  method siguiente() = nivel3
  method iniciar() {
    game.boardGround(self.background())
    levelManager.nivelActual(self)

    const enemigo2 = new Enemigo(id = 825, position = game.at(0, 12), vida = 3, velocidad = 300)
    enemigo2.iniciar()

    levelManager.enemigosVivos(1)
  }
}

object nivel3 {
  method background() = "Background_forest.png"
  method siguiente() = pantallaFinal
  method iniciar() {
    game.boardGround(self.background())
    levelManager.nivelActual(self)

    const enemigo4 = new Enemigo(id = 333, position = game.at(0, 12), vida = 3, velocidad = 300)
    enemigo4.iniciar()
    
    const enemigo5 = new Enemigo(id = 444, position = game.at(11, 10), vida = 3, velocidad = 300)
    enemigo5.iniciar()

    levelManager.enemigosVivos(2)
  }
}